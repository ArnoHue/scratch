when I receive "DrawBoard"
DrawBoard 0 0

when I receive "SelectPromotionPieceWhite"
broadcast "ModalDialogBegin" and wait
switch costume to "Panel"
go to x: -60 y: 0
stamp
if IsPlayerBlack = 0 then
	go to x: -120 y: 0
	switch costume to "WhiteQueen"
	stamp
	go to x: -80 y: 0
	switch costume to "WhiteRook"
	stamp
	go to x: -40 y: 0
	switch costume to "WhiteBishop"
	stamp
	go to x: 0 y: 0
	switch costume to "WhiteKnight"
	stamp
else
	go to x: -120 y: 0
	switch costume to "BlackQueen"
	stamp
	go to x: -80 y: 0
	switch costume to "BlackRook"
	stamp
	go to x: -40 y: 0
	switch costume to "BlackBishop"
	stamp
	go to x: 0 y: 0
	switch costume to "BlackKnight"
	stamp
end
repeat until mouse down? and mouse x > -140 and mouse x < 20 and mouse y > -20 and mouse y < 20
	wait 0.1 secs
end
if mouse x < -100 then
	set SelectedPromotionPiece to WhiteQueen
else
	if mouse x < -60 then
		set SelectedPromotionPiece to WhiteRook
	else
		if mouse x < -20 then
			set SelectedPromotionPiece to WhiteBishop
		else
			set SelectedPromotionPiece to WhiteKnight
		end
	end
end
DrawBoard 0 0
broadcast "ModalDialogEnd" and wait

define DrawVirtualPiece (Idx arg) (Piece arg)
if IsPlayerBlack = 0 then
	DrawPiece (Idx arg) (Piece arg)
else
	DrawPiece (Idx arg) (Piece arg * -1)
end

define-atomic DrawPiece (Idx arg) (Piece arg)
set x to -210 + (Idx arg - 1) mod 8 * 42.5
set y to 148 - (Idx arg - 1 - (Idx arg - 1) mod 8) / 8 * 42.5
SelectCostume (Piece arg)
stamp

define MovePieceImpl (FromIdx) (ToIdx)
if IsPlayerBlack = 0 then
	SelectCostume (item FromIdx of Board)
else
	SelectCostume (item FromIdx of Board * -1)
end
set x to -210 + (FromIdx - 1) mod 8 * 42.5
set y to 148 - (FromIdx - 1 - (FromIdx - 1) mod 8) / 8 * 42.5
show
glide 0.5 secs to x: -210 + (ToIdx - 1) mod 8 * 42.5 y: 148 - (ToIdx - 1 - (ToIdx - 1) mod 8) / 8 * 42.5
stamp
hide

define-atomic SelectCostume (Piece arg)
switch costume to "NoPiece"
if Piece arg = BlackBishop then
	switch costume to "BlackBishop"
end
if Piece arg = BlackPawn then
	switch costume to "BlackPawn"
end
if Piece arg = BlackQueen then
	switch costume to "BlackQueen"
end
if Piece arg = BlackKing then
	switch costume to "BlackKing"
end
if Piece arg = BlackKnight then
	switch costume to "BlackKnight"
end
if Piece arg = BlackRook then
	switch costume to "BlackRook"
end
if Piece arg = WhiteBishop then
	switch costume to "WhiteBishop"
end
if Piece arg = WhiteQueen then
	switch costume to "WhiteQueen"
end
if Piece arg = WhiteKing then
	switch costume to "WhiteKing"
end
if Piece arg = WhitePawn then
	switch costume to "WhitePawn"
end
if Piece arg = WhiteKnight then
	switch costume to "WhiteKnight"
end
if Piece arg = WhiteRook then
	switch costume to "WhiteRook"
end

define-atomic DrawBoard (ExclIdx1) (ExcldIdx2)
clear
broadcast "ProgressValueChanged"
set Idx to 1
repeat 64
	if not Idx = ExclIdx1 and not Idx = ExcldIdx2 then
		if not item Idx of Board = Empty then
			DrawVirtualPiece (Idx) (item Idx of Board)
		end
	end
	change Idx by 1
end

when I receive "MovePiece"
if length of MovePieceSpecial = 0 then
	DrawBoard (MovePieceFromIdx) 0
	MovePieceImpl (MovePieceFromIdx) (MovePieceToIdx)
else
	if MovePieceSpecial = WhiteKingsideCastling then
		DrawBoard (item 61 of VirtualBoard) 0
		MovePieceImpl (item 61 of VirtualBoard) (item 63 of VirtualBoard)
		DrawBoard (item 61 of VirtualBoard) (item 64 of VirtualBoard)
		DrawVirtualPiece (item 63 of VirtualBoard) (WhiteKing)
		MovePieceImpl (item 64 of VirtualBoard) (item 62 of VirtualBoard)
	end
	if MovePieceSpecial = WhiteQueensideCastling then
		DrawBoard (item 61 of VirtualBoard) 0
		MovePieceImpl (item 61 of VirtualBoard) (item 59 of VirtualBoard)
		DrawBoard (item 61 of VirtualBoard) (item 57 of VirtualBoard)
		DrawVirtualPiece (item 59 of VirtualBoard) (WhiteKing)
		MovePieceImpl (item 57 of VirtualBoard) (item 60 of VirtualBoard)
	end
	if MovePieceSpecial = BlackKingsideCastling then
		DrawBoard (item 5 of VirtualBoard) 0
		MovePieceImpl (item 5 of VirtualBoard) (item 7 of VirtualBoard)
		DrawBoard (item 5 of VirtualBoard) (item 8 of VirtualBoard)
		DrawVirtualPiece (item 7 of VirtualBoard) (BlackKing)
		MovePieceImpl (item 8 of VirtualBoard) (item 6 of VirtualBoard)
	end
	if MovePieceSpecial = BlackQueensideCastling then
		DrawBoard (item 5 of VirtualBoard) 0
		MovePieceImpl (item 5 of VirtualBoard) (item 3 of VirtualBoard)
		DrawBoard (item 1 of VirtualBoard) (item 5 of VirtualBoard)
		DrawVirtualPiece (item 3 of VirtualBoard) (BlackKing)
		MovePieceImpl (item 1 of VirtualBoard) (item 4 of VirtualBoard)
	end
end
set MovePieceFromIdx to -1
set MovePieceToIdx to -1
set MoveFromIdx to -1
set MoveToIdx to -1

when flag clicked
hide
set MovePieceFromIdx to -1
set MovePieceToIdx to -1
